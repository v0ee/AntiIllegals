plugins {
    id 'java-library'
    id 'io.github.goooler.shadow' version '8.1.8'
    id 'xyz.jpenilla.run-paper' version '3.0.2'
}

group = 'org.zeroBzeroT'
version = '1.1.13'
description = 'A plugin for Minecraft servers. Restricts certain block placements and removes illegals from player inventories.'

ext {
    minecraftVersion = project.findProperty('minecraftVersion') ?: '1.20.4'
    javaVersion = JavaVersion.toVersion(project.findProperty('javaVersion') ?: '21')
}

repositories {
    mavenCentral()
    maven {
        // PaperMC
        url = 'https://repo.papermc.io/repository/maven-public/'
        content {
            includeGroup 'com.mojang'
            includeGroup 'dev.folia'
            includeGroup 'io.papermc.paper'
            includeGroup 'net.kyori'
            includeGroup 'net.md-5'
        }
    }
    maven {
        // Item-NBT-API
        url = 'https://repo.codemc.org/repository/maven-public/'
        content {
            includeGroup 'de.tr7zw'
        }
    }
    maven {
        // FoliaLib
        url = 'https://nexuslite.gcnt.net/repos/other/'
        content {
            includeGroup 'com.tcoded'
        }
    }
}

dependencies {
    // Paper API (provided scope)
    // https://jd.papermc.io/paper/1.20/
    compileOnly group: 'io.papermc.paper', name: 'paper-api', version: "${minecraftVersion}-R0.1-SNAPSHOT"

    // Folia API
    // https://jd.papermc.io/folia/1.20/
    //compileOnly group: 'dev.folia', name: 'folia-api', version: "${project.minecraft_version}-R0.1-SNAPSHOT"

    // Other dependencies
    shadow 'de.tr7zw:item-nbt-api:2.15.2'
    shadow 'org.bstats:bstats-bukkit:3.1.0'
}

java {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = javaVersion.majorVersion.toInteger()
}

processResources {
    filteringCharset "UTF-8"
    filesMatching("plugin.yml") {
        filter { line -> line.replace("@VERSION@", project.version) }
        filter { line -> line.replace("@DESCRIPTION@", project.description) }
    }
}

// https://imperceptiblethoughts.com/shadow/
shadowJar {
    configurations = [project.configurations.shadow]
    archiveFileName = "${project.name}-${project.version}+${minecraftVersion}.jar"
    destinationDirectory = layout.buildDirectory.dir('dist')

    // Relocations (shading) setup
    relocate('de.tr7zw.changeme.nbtapi', 'org.zeroBzeroT.tr7zw.nbtapi')
    relocate('org.bstats', 'org.zeroBzeroT.bstats')
    relocate('com.tcoded.folialib', 'org.zeroBzeroT.tcoded.folialib')

    // delete unshaded jar
    doLast {
        file(jar.archiveFile).delete()
    }
}
build.finalizedBy(shadowJar)

tasks {
    runServer {
        runDirectory = layout.projectDirectory.dir('run')
        minecraftVersion(minecraftVersion)
        dependsOn('build')
        systemProperty('com.mojang.eula.agree', 'true')
        downloadPlugins {
            url("https://github.com/ViaVersion/ViaBackwards/releases/download/5.5.0/ViaBackwards-5.5.0.jar")
            url("https://github.com/ViaVersion/ViaVersion/releases/download/5.5.0/ViaVersion-5.5.0.jar")
            url("https://github.com/dmulloy2/ProtocolLib/releases/download/5.4.0/ProtocolLib.jar")
            url("https://ci.codemc.io/job/Updated-NoCheatPlus/job/Updated-NoCheatPlus/198/artifact/target/NoCheatPlus.jar")
        }
    }
}
